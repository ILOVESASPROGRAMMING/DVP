***关联逻辑库并读取format***;
LIBNAME RAWDATA "E:\SAS_DATA\KH_20230114"; 
OPTIONS FMTSEARCH=(RAWDATA); 
***关联逻辑库并读取format***;

***非条件缺失宏***;

%MACRO UNCONDITIONAL_MISSING(OUT=,DATA=,VARIABLE=);
DATA &OUT;
LENGTH NAME$20. LABEL$20. QUERY$50.;
SET RAWDATA.&DATA;

	IF MISSING(&VARIABLE)=1 THEN DO;
		NAME=VNAME(&VARIABLE);
		LABEL=LABEL(&VARIABLE);
		QUERY="缺失，请补充。";
		OUTPUT;
	END;

KEEP siteid subjectid studyevent visit name query label CRFname ItemGroupRepeatKey;
RUN;

%MEND;

***非条件缺失宏***;

***生成表达式及判别宏，在条件缺失宏和时间窗宏中调用***;

%MACRO EXPRESSION_JUDGE(VARS=,RELS=,VALUES=,NAME=,STR=); *该宏生成表达式，以及在表达式为真时输出质疑;

	*清除多余空格;
	%LET VARS=%SYSFUNC(COMPBL(&VARS));
	%LET RELS=%SYSFUNC(COMPBL(&RELS));
	%LET VALUES=%SYSFUNC(COMPBL(&VALUES));

	*生成第一个表达式;
	%LET NUM=1;
	%LET VAR_EXPRESSION=%SCAN(&VARS,&NUM,"/");
	%LET REL_EXPRESSION=%SCAN(&RELS,&NUM,"/");
	%LET VAL_EXPRESSION=%SCAN(&VALUES,&NUM,"/");
	%LET EXPRESSION=(&VAR_EXPRESSION &REL_EXPRESSION &VAL_EXPRESSION);

	*计算变量数量来决定DOWHILE循环的终止节点;
	%LET NUM_OF_VARS=%SYSFUNC(COUNTW(&VARS,"/"));

	*输出到日志确认数据是否正确;
	%PUT &NUM_OF_VARS;
	%PUT &EXPRESSION;

	*生成下一个表达式并与上一个表达式进行拼接;
	%DO %WHILE (&NUM LT &NUM_OF_VARS);

		%LET NUM=%EVAL(&NUM+1);
		%LET VAR_EXPRESSION=%SCAN(&VARS,&NUM,"/");
		%LET REL_EXPRESSION=%SCAN(&RELS,&NUM,"/");
		%LET VAL_EXPRESSION=%SCAN(&VALUES,&NUM,"/");

		%LET EXPRESSION=%SYSFUNC(CATX("AND","&EXPRESSION","(&VAR_EXPRESSION &REL_EXPRESSION &VAL_EXPRESSION)"));
		%let EXPRESSION=%SYSFUNC(COMPRESS(&EXPRESSION,'"'));

		*输出到日志确认数据是否正确;
		%PUT &EXPRESSION;

	%END;

	*表达式为真时输出质疑;
	IF &EXPRESSION THEN DO;
		NAME=VNAME(&NAME);
		LABEL=LABEL(&NAME);			
		QUERY=COMPRESS("&STR");
		ANSWER="";
		OUTPUT;	
	END;

%MEND;

***生成表达式及判别宏，在条件缺失宏和时间窗宏中调用***;

***条件缺失宏***;

%MACRO CONDITIONAL_MISSING(CATALOG=0,DATA_CONDITION=,DATA_MISSING=,CRFNAME=,OUT=,VARS=,RELS=,VALUES=,NAME=,STR=); *该宏生成所需数据集，并使用宏EXPRESSION_JUDGE输出质疑文本;

	DATA &OUT;
		LABEL siteid='中心编号' subjectid='受试者编号' StudyEvent='访视' CRFNAME='CRF表名称' ItemGroupRepeatKey='CRF表行号' NAME='变量' label='变量标签' QUERY='问题描述';
		LENGTH CRFNAME $100 NAME $600 QUERY $1000;
		IF &CATALOG=0 THEN DO; *变量CATALOG的值为0（默认值）时MERGE BY SUBJECTID STUDENTEVENT;
			MERGE RAWDATA.&DATA_CONDITION RAWDATA.&DATA_MISSING;
			BY subjectid StudyEvent;
		END;
		IF &CATALOG=1 THEN DO; *变量CATALOG的值为1（默认值）时MERGE BY SUBJECTID;
			MERGE RAWDATA.&DATA_CONDITION RAWDATA.&DATA_MISSING;
			BY subjectid;
		END;
		CRFNAME="&CRFNAME";
		%EXPRESSION_JUDGE(VARS=&VARS,RELS=&RELS,VALUES=&VALUES,NAME=&NAME,STR=&STR); *生成表达式，以及在表达式为真时输出质疑;
		KEEP siteid SubjectID StudyEvent CRFNAME ItemGroupRepeatKey NAME label QUERY ANSWER;
	RUN;

%MEND;

***条件缺失宏***;

***时间窗宏***;

%MACRO QUERY_TIMWINDOW(CATALOG=0,DATA1=,DATA2=,CRFNAME=,OUT=,VAR1=,VAR2=,VARS=,RELS=,VALUES=,NAME=,STR=); *该宏清理时间数据，生成所需数据集，并使用宏EXPRESSION_JUDGE输出质疑文本;

*判断日期变量VAR1的类型，结果储存在宏变量RESULT1;
DATA INSET_1; 
	SET RAWDATA.&DATA1(OBS=1);
	TYPE_1=VTYPE(&VAR1);
	IF TYPE_1='C' THEN TYPE_1_1=1;
	IF TYPE_1='N' THEN TYPE_1_1=2;
	call symput("RESULT1",TYPE_1_1);
RUN;
%PUT &RESULT1; *检查宏变量RESULT1是否正常工作;

*判断日期变量VAR2的类型，结果储存在宏变量RESULT2;
DATA INSET_2;
	SET RAWDATA.&DATA2(OBS=1);
	TYPE_2=VTYPE(&VAR2);
	IF TYPE_2='C' THEN TYPE_2_2=1;
	IF TYPE_2='N' THEN TYPE_2_2=2;
	call symput("RESULT2",TYPE_2_2);
RUN;
%PUT &RESULT2; *检查宏变量RESULT2是否正常工作;

DATA INSET; *清理时间数据，生成所需数据集;
	
	IF &CATALOG=0 THEN DO; *变量CATALOG的值为0（默认值）时MERGE BY SUBJECTID STUDENTEVENT;
		MERGE RAWDATA.&DATA1 RAWDATA.&DATA2;
		BY subjectID StudyEvent;
	END;
	IF &CATALOG=1 THEN DO; *变量CATALOG的值为1（默认值）时MERGE BY SUBJECTID;
		MERGE RAWDATA.&DATA1 RAWDATA.&DATA2;
		BY subjectid;
	END;

	IF CMISS(&VAR1.,&VAR2.) NE 0 THEN DELETE;
	IF FIND(&VAR1,"K")^=0 OR FIND(&VAR2,"K")^=0 THEN DELETE;

	IF (&result1=1 AND &result2=1) THEN DO; *当VAR1为字符型VAR2为字符型时将VAR1VAR2都转化成数值型;
		DATE1=INPUT(SUBSTR(KCOMPRESS(&VAR1),1,10),YYMMDD10.);
		DATE2=INPUT(SUBSTR(KCOMPRESS(&VAR2),1,10),YYMMDD10.);
		IF CMISS(DATE1,DATE2) NE 0 THEN DELETE;
		FORMAT DATE1 DATE2 YYMMDD10.;
	END;
	IF (&result1=1 AND &result2=2) THEN DO; *当VAR1为字符型VAR2为数值型时将VAR1转化成数值型;
		DATE1=INPUT(SUBSTR(KCOMPRESS(&VAR1),1,10),YYMMDD10.);
		DATE2=&VAR2;
		IF CMISS(DATE1,DATE2) NE 0 THEN DELETE;
		FORMAT DATE1 DATE2 YYMMDD10.;
	END;
	IF (&result1=2 AND &result2=1) THEN DO; *当VAR1为数值型VAR2为字符型时将VAR2转化成数值型;
		DATE1=&VAR1;
		DATE2=INPUT(SUBSTR(KCOMPRESS(&VAR2),1,10),YYMMDD10.);
		IF CMISS(DATE1,DATE2) NE 0 THEN DELETE;
		FORMAT DATE1 DATE2 YYMMDD10.;
	END;

RUN;
	
DATA &OUT; *利用宏EXPRESSION_JUDGE输出质疑文本;

	LABEL subjectID='受试者编号' StudyEvent='访视' CRFNAME='CRF表名称' ItemGroupRepeatKey='CRF表行号' NAME='变量' label='变量标签' QUERY='问题描述';
	LENGTH CRFNAME $100 NAME $60 QUERY $1000;
	SET INSET;
	CRFNAME="&CRFNAME";
	%EXPRESSION_JUDGE(VARS=&VARS,RELS=&RELS,VALUES=&VALUES,NAME=&NAME,STR=&STR);
	KEEP siteid subjectid StudyEvent CRFNAME ItemGroupRepeatKey NAME label QUERY VISIT;

RUN;

%MEND;

***时间窗宏***;


