%MACRO EXPRESSION_JUDGE(VARS=,RELS=,VALUES=,NAME=,STR=); *该宏生成表达式，以及在表达式为真时输出质疑;

*清除多余空格;
%LET VARS=%SYSFUNC(COMPBL(&VARS));
%LET RELS=%SYSFUNC(COMPBL(&RELS));
%LET VALUES=%SYSFUNC(COMPBL(&VALUES));

*生成第一个表达式;
%LET NUM=1;
%LET VAR_EXPRESSION=%SCAN(&VARS,&NUM,"/");
%LET REL_EXPRESSION=%SCAN(&RELS,&NUM,"/");
%LET VAL_EXPRESSION=%SCAN(&VALUES,&NUM,"/");
%LET EXPRESSION=(&VAR_EXPRESSION &REL_EXPRESSION &VAL_EXPRESSION);

*计算变量数量来决定DOWHILE循环的终止节点;
%LET NUM_OF_VARS=%SYSFUNC(COUNTW(&VARS,"/"));

*输出到日志确认数据是否正确;
%PUT &NUM_OF_VARS;
%PUT &EXPRESSION;

*生成下一个表达式并与上一个表达式进行拼接;
%DO %WHILE (&NUM LT &NUM_OF_VARS);

	%LET NUM=%EVAL(&NUM+1);
	%LET VAR_EXPRESSION=%SCAN(&VARS,&NUM,"/");
	%LET REL_EXPRESSION=%SCAN(&RELS,&NUM,"/");
	%LET VAL_EXPRESSION=%SCAN(&VALUES,&NUM,"/");

	%LET EXPRESSION=%SYSFUNC(CATX("AND","&EXPRESSION","(&VAR_EXPRESSION &REL_EXPRESSION &VAL_EXPRESSION)"));
	%let EXPRESSION=%SYSFUNC(COMPRESS(&EXPRESSION,'"'));

	*输出到日志确认数据是否正确;
	%PUT &EXPRESSION;

%END;

*表达式为真时输出质疑;
IF &EXPRESSION THEN DO;
	NAME=VNAME(&NAME);
	LABEL=LABEL(&NAME);			
	QUERY=COMPRESS("&STR");
	ANSWER="";
	OUTPUT;	
END;

%MEND;




%MACRO CONDITIONAL_MISSING(CATALOG=0,DATA_CONDITION=,DATA_MISSING=,CRFNAME=,OUT=,VARS=,RELS=,VALUES=,NAME=,STR=); *该宏生成所需数据集，并使用宏EXPRESSION_JUDGE;

	DATA &OUT;
		LABEL siteid='中心编号' subjectid='受试者编号' StudyEvent='访视' CRFNAME='CRF表名称' ItemGroupRepeatKey='CRF表行号' NAME='变量' label='变量标签' QUERY='问题描述';
		LENGTH CRFNAME $100 NAME $600 QUERY $1000;
		%IF &CATALOG=0 %THEN %DO; *变量CATALOG的值为0（默认值）时MERGE BY SUBJECTID STUDENTEVENT;
			MERGE RAWDATA.&DATA_CONDITION RAWDATA.&DATA_MISSING;
			BY subjectid StudyEvent;
		%END;
		%IF &CATALOG=1 %THEN %DO; *变量CATALOG的值为0（默认值）时MERGE BY SUBJECTID STUDENTEVENT;
			MERGE RAWDATA.&DATA_CONDITION RAWDATA.&DATA_MISSING;
			BY subjectid;
		%END;
		CRFNAME="&CRFNAME";
		%EXPRESSION_JUDGE(VARS=&VARS,RELS=&RELS,VALUES=&VALUES,NAME=&NAME,STR=&STR); *生成表达式，以及在表达式为真时输出质疑;
		KEEP siteid SubjectID StudyEvent CRFNAME ItemGroupRepeatKey NAME label QUERY ANSWER;
	RUN;

%MEND;
